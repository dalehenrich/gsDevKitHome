TDScriptLeafNode{#name:'bench',#contents:'[ :topez :objIn :tokens :command :commandNode | 
  | opts args |
  \"for help: ./bench -h\"
  command
    getOptsMixedLongShort:
      {#(\'help\' $h #\'none\').
      #(\'bench\' nil #\'required\').
      #(\'flood\' nil #\'none\').
      #(\'profile\' nil #\'none\')}
    optionsAndArguments: [ :options :operands | 
      opts := options.
      args := operands ].
  opts
    at: \'help\'
    ifAbsent: [ 
      | kermit beforePages afterPages time stream bob benchmarkBlocks benchmarks preBuiltQuery1 preBuiltQuery2 preBuiltQuery3 |
      kermit := ((TDSymbolDictionaryDefinition new
        symbolDictionaryName: #\'UserGlobals\';
        username: \'DataCurator\';
        yourself) symbolList at: 1) at: #\'Kermit\'.
      stream := WriteStream on: String new.
      opts
        at: \'flood\'
        ifPresent: [ :arg | 
          | pagesToRead oop oopCount |
          pagesToRead := (System stoneConfigurationAt: #\'SHR_PAGE_CACHE_SIZE_KB\')
            // 10.
          beforePages := System pageReads.
          oop := 1.
          oopCount := 0.
          time := Time
            millisecondsElapsedTime: [ 
              [ 
              afterPages := System pageReads.
              afterPages - beforePages < pagesToRead
                and: [ oop < System _oopHighWaterMark ] ]
                whileTrue: [ 
                  [ 
                  (Object _objectForOop: oop) class.
                  oopCount := oopCount + 1 ]
                    on: Error
                    do: [ :ex | ex return ].
                  oop := oop + 8 ] ].
          stream
            nextPutAll: \'Read ~\';
            print: oopCount // 1000000;
            nextPutAll: \' million objects in ~\';
            print: time // 1000;
            nextPutAll: \' seconds to flood cache.\';
            lf;
            yourself ].
      preBuiltQuery1 := GsQuery
        fromString: \'(each.submittedBy == bob) & (each.assignedTo == bob)\'
        on: kermit current _bugs.
      preBuiltQuery1 bind: \'bob\' to: (kermit current userWithId: \'bretlb\').
      preBuiltQuery3 := preBuiltQuery1 copy
        queryOptions: GsQuery defaultQueryOptions - GsQueryOptions autoOptimize.
      preBuiltQuery2 := GsQuery
        fromString: \'(each.assignedTo == bob) & (each.submittedBy == bob)\'
        on: kermit current _bugs.
      preBuiltQuery2 bind: \'bob\' to: (kermit current userWithId: \'bretlb\').
      benchmarkBlocks := Dictionary new
        at: \'1\'
          put:
            {[ 
              | a b c |
              a := kermit current _bugs
                select: { :each | each.submittedBy == bob }.\t\"512\"
              b := kermit current _bugs
                select: { :each | each.assignedTo == bob }.\t\"1923\"
              c := a * b ].
              \'17\'.
              \'2.91\'};
        at: \'2\'
          put:
            {[ 
              kermit current _bugs
                select: { :each | (each.submittedBy == bob) & (each.assignedTo == bob) } ].
              \'607\'.
              \'3.41\'};
        at: \'3\'
          put:
            {[ 
              kermit current _bugs
                select: { :each | (each.assignedTo == bob) & (each.submittedBy == bob) } ].
              \'17\'.
              \'2.86\'};
        at: \'4\'
          put:
            {[ 
              | query |
              query := GsQuery
                fromString:
                  \'(each.submittedBy == bob) & (each.assignedTo == bob)\'
                on: kermit current _bugs.
              query bind: \'bob\' to: bob.
              query queryResult ].
              \'623\'.
              \'12.43\'};
        at: \'5\'
          put:
            {[ 
              | query |
              query := GsQuery
                fromString:
                  \'(each.assignedTo == bob) & (each.submittedBy == bob)\'
                on: kermit current _bugs.
              query bind: \'bob\' to: bob.
              query queryResult ].
              \'1538\'.
              \'17.68\'};
        at: \'6\'
          put:
            {[ preBuiltQuery1 queryResult ].
              \'33\'.
              \'3.77\'};
        at: \'6`\'
          put:
            {[ preBuiltQuery3 queryResult ].
              \'??\'.
              \'faster that 6?\'};
        at: \'7\'
          put:
            {[ preBuiltQuery2 queryResult ].
              \'1660\'.
              \'38.70\'};
        yourself.
      benchmarks := {}.
      opts
        at: \'bench\'
        ifPresent: [ :benchmarkId | 
          benchmarkId = \'all\'
            ifTrue: [ 
              benchmarks := benchmarkBlocks associations asArray
                sorted: [ :a :b | a key < b key ] ]
            ifFalse: [ benchmarks add: (benchmarkBlocks associationAt: benchmarkId) ] ]
        ifAbsent: [ benchmarks add: \'1\' -> (benchmarkBlocks at: \'1\') ].
      opts
        at: \'profile\'
        ifPresent: [ :arg | topez evaluateCommandString: \'pf start\' ].
      benchmarks
        do: [ :assoc | 
          | benchmarkName block expectedPageReads expectedElapsedSeconds strm |
          benchmarkName := assoc key.
          Transcript
            show: \'running benchmark \' , benchmarkName printString;
            cr.
          block := assoc value at: 1.
          expectedPageReads := assoc value at: 2.
          expectedElapsedSeconds := assoc value at: 3.
          beforePages := System pageReads.
          bob := kermit current userWithId: \'bretlb\'.
          time := Time millisecondsElapsedTime: [ 10000 timesRepeat: block ].
          afterPages := System pageReads.
          strm := WriteStream on: String new.
          strm
            nextPutAll: benchmarkName printString , \' took \';
            print: time;
            nextPutAll: \' ms and read \';
            print: afterPages - beforePages;
            nextPutAll: \' pages to perform queries.\';
            cr;
            tab;
            nextPutAll:
                \'expected: \' , expectedElapsedSeconds , \' seconds and \' , expectedPageReads
                    , \' page reads.\'.
          Transcript
            show: strm contents;
            cr.
          stream
            nextPutAll: strm contents;
            cr ].
      opts
        at: \'profile\'
        ifPresent: [ :arg | topez evaluateCommandString: \'pf stop\' ].
      stream contents ]
    ifPresent: [ :ignored | 
      TDManPage
        viewManPage:
          \'NAME
  bench - bench sript utility template
SYNOPSIS
  bench [-h|--help] [--flood] [--bench=[ all|<benchmark-id> ] ] --profile
DESCRIPTION
  Run benchmarks against indexes in kermit.

OPTIONS
  flood
    flood the cache with random pages (only use with a relatively small SPC (500M))
  bench=[all|<benchmark-id>]
    all - produce report for all benchmarks
    <benchmark-id> - produce report for given benchmark (1-7)
  profile
    run profiler during execution of benchmark block(`pf start` ... `pf stop`). Use
    `pf view` to look at profiler results

EXAMPLES
  ./bench -h
  ./bench --flood
  ./bench
  ./bench --bench=1; edit
  ./bench --bench=all; edit
  ./bench --bench=6 --profile; edit; pf view
\'
        topez: topez ] ]',#creationTime:DateAndTime['2014-07-29T10:08:40.3795459270477-07:00'],#modificationTime:DateAndTime['2014-07-29T17:28:28.1884870529174-07:00']}